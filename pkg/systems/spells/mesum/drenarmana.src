    use uo;
use os;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedScripts:timedScripts";

program SpellScript(params)
	var who := params[1];
	var targ := params[2];
	params := 0; // No longer needed

	if (!targ.isa(POLCLASS_MOBILE))
		SendSysMessageEx(who, "Isto nao pode ser alvejado por essa magia.", SSM_FAIL);
		return;
	endif


	PlaySoundEffect(targ, SFX_SPELL_WEAKEN);

	var time := 0;
	var oldx := who.x;
	var oldy := who.y;

	who.frozen := 1;

	var amt := 10;
	if (temHabilidade(who, "Maldicao do Sangue de Asan"))
		if (isBloodied(who))
			amt := 15;
		endif
	endif

	Corromper(who, 8, targ);
	while  (time < 10)

		if ( (who.x != oldx) || (who.y != oldy))
			SendSysMEssageEx(who, "Voce se moveu e cancelou a magia.", SSM_FAIL);
			break;
		endif

		if (Distance(who, targ) >= 16)
			SendSysMessageEx(who, "O alvo se distanciou muito.", SSM_FAIL);
			break;
		endif

		if(targ.dead)
			break;
		endif

		var targmana := AP_GetVital(targ, MANA);
		if (targmana < amt)
			break;
		endif

		AP_SetVital(targ, MANA, (targmana-amt));

		var mymana := AP_GetVital(who, MANA) + amt;
		var maxmana := AP_GetVitalMaximumValue(who, MANA);

		if (mymana > maxmana)
			mymana := maxmana;
		endif

		AP_SetVital(who, MANA, mymana);

		PlayMovingEffectEx(targ, who, 3982, 255, 1, 2119);	
		time := time + 1;
		PerformAction(who, ANIM_CAST_AREA);
		sleep(1);
	endwhile

	who.frozen := 0;
	
	return 1;
endprogram 
