    use uo;
use os;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedScripts:timedScripts";

program SpellScript(params)
	var who := params[1];
	var targ := params[2];
	params := 0; // No longer needed

	if (!targ.isa(POLCLASS_MOBILE))
		SendSysMessageEx(who, "Isto nao pode ser alvejado por essa magia.", SSM_FAIL);
		return;
	endif


	PlaySoundEffect(targ, SFX_SPELL_WEAKEN);

	var time := 0;
	var oldx := who.x;
	var oldy := who.y;

	who.frozen := 1;
	
	var amt := 10;
	if (temHabilidade(who, "Maldicao do Sangue de Asan"))
		if (isBloodied(who))
			amt := 15;
		endif
	endif

	Corromper(who, 9, targ);
	while  (time < 10)

		if ( (who.x != oldx) || (who.y != oldy))
			SendSysMEssageEx(who, "Voce se moveu e cancelou a magia.", SSM_FAIL);
			break;
		endif

		if (Distance(who, targ) >= 16)
			SendSysMessageEx(who, "O alvo se distanciou muito.", SSM_FAIL);
			break;
		endif

		if(targ.dead)
			break;
		endif

		//ApplyRawDamage( targ, 10, 0, DAMAGE_SHOW );
		ApplyRawDamage( targ, amt );
		HealDamage( who, amt);
		
		if(targ.dead)
			break;
		endif

		PlayMovingEffectEx(targ, who, 3982, 255, 1, 2117);
		PerformAction(who, ANIM_CAST_AREA);
		time := time + 1;
		sleep(1);
	endwhile

	who.frozen := 0;
	
	return 1;
endprogram 
